-- Tests for the grants and math patch

Macro FlywheelController price=1.0 borrowRate=0.000005 vtxInitAmount=5000000e18
    Unitroller Deploy
    PriceOracle Deploy Fixed price
    PriceOracleProxy Deploy Admin (PriceOracle Address) (Address Zero) (Address Zero) (Address Zero) (Address Zero) (Address Zero)
    ----g2
    ControllerImpl Deploy ScenarioG2 ControllerScenG2
    Unitroller SetPendingImpl ControllerScenG2
    ControllerImpl ControllerScenG2 BecomeG2
    --list some tokens
    Controller SetPriceOracle (PriceOracleProxy Address)
    Controller SetMaxAssets 20
    Controller SetCloseFactor 0.5
    Controller LiquidationIncentive 1.1
    NewVToken ZRX cZRX
    NewVToken BAT vBAT
    Support cZRX collateralFactor:0.5
    Support vBAT collateralFactor:0.5
    -- final
    ControllerImpl Deploy ScenarioG3 ControllerScen
    Unitroller SetPendingImpl ControllerScen
    ControllerImpl ControllerScen BecomeG3 1e18 [cZRX vBAT]
    Erc20 Deploy Standard VTX "VTX Token" 18
    Give (Address Controller) vtxInitAmount VTX

Macro GrantsController
    FlywheelController
    -- g4
    ControllerImpl Deploy ScenarioG4 ControllerScen
    Unitroller SetPendingImpl ControllerScen
    ControllerImpl ControllerScen BecomeG4
    -- g5
    ControllerImpl Deploy ScenarioG5 ControllerScen
    Unitroller SetPendingImpl ControllerScen
    ControllerImpl ControllerScen BecomeG5
    -- g6
    ControllerImpl Deploy ScenarioG6 ControllerScen
    Unitroller SetPendingImpl ControllerScen
    ControllerImpl ControllerScen BecomeG6
    -- current
    ControllerImpl Deploy Scenario ControllerScen
    Unitroller SetPendingImpl ControllerScen
    ControllerImpl ControllerScen Become

Macro InitSpeeds
    Prep Geoff 100e18 ZRX cZRX
    Mint Geoff 50e18 cZRX--tokenbalance = 50e18 / 2e9 = 2.5e10
    Prep Coburn Some BAT vBAT
    Mint Coburn 6e18 vBAT--tokenbalance = 6e18 / 2e9 = 3e9
    EnterMarkets Coburn vBAT
    Borrow Coburn 1e18 cZRX
    Controller SetVtxSpeed cZRX 1
    Controller SetVtxSpeed vBAT 1
    Controller RefreshVtxSpeeds
    Controller Send "setVtxAddress(address)" (Address VTX)

Test "VTX can be granted in combination with liquidity rewards"
    GrantsController
    InitSpeeds
    Assert Equal (Controller VtxAccrued Geoff) 0
    Assert Equal (Erc20 VTX TokenBalance Geoff) 0
    FastForward 1000 Blocks
    Controller ClaimVtx Geoff
    Controller Send "_grantVtx(address,uint256)" (Address Geoff) 1000
    Assert Equal (Controller VtxAccrued Geoff) 0
    Assert Equal (Erc20 VTX TokenBalance Geoff) 1000000000000000001000

Test "VTX can be granted"
    -- Can be granted once
    GrantsController
    InitSpeeds
    Assert Equal (Controller VtxAccrued Geoff) 0
    Assert Equal (Erc20 VTX TokenBalance Geoff) 0
    Controller Send "_grantVtx(address,uint256)" (Address Geoff) 1000
    Assert Equal (Controller VtxAccrued Geoff) 0
    Assert Equal (Erc20 VTX TokenBalance Geoff) 1000
    -- Assert Log VtxGranted (recipient (Address Geoff)) (amount "1000")
    -- Can be granted multiple times
    Controller Send "_grantVtx(address,uint256)" (Address Geoff) 2000
    Assert Equal (Controller VtxAccrued Geoff) 0
    Assert Equal (Erc20 VTX TokenBalance Geoff) 3000

Test "VTX can be streamed to contributors"
    GrantsController
    InitSpeeds
    Assert Equal (Controller VtxAccrued Torrey) 0
    Assert Equal (Erc20 VTX TokenBalance Torrey) 0
    Controller Send "_setContributorVtxSpeed(address,uint256)" (Address Torrey) 300
    -- Assert Log ContributorVtxSpeedUpdated (recipient (Address Torrey)) (amount "300")
    FastForward 1000 Blocks
    -- Just claimVtx does not receive VTX
    Controller ClaimVtx Torrey
    Assert Equal (Controller VtxAccrued Torrey) 0
    Assert Equal (Erc20 VTX TokenBalance Torrey) 0
    -- Calling updateContributorRewards and then claimVtx receives VTX
    Controller UpdateContributorRewards Torrey
    Assert Equal (Controller VtxAccrued Torrey) 300000
    Controller ClaimVtx Torrey
    Assert Equal (Controller VtxAccrued Torrey) 0
    Assert Equal (Erc20 VTX TokenBalance Torrey) 300000

Test "VTX can be streamed in combination with liquidity rewards"
    GrantsController
    InitSpeeds
    Controller Send "_setContributorVtxSpeed(address,uint256)" (Address Geoff) 300
    FastForward 1000 Blocks
    -- Just claimVtx does not receive VTX
    Controller UpdateContributorRewards Geoff
    Assert Equal (Controller VtxAccrued Geoff) 300000
    Controller ClaimVtx Geoff
    Assert Equal (Controller VtxAccrued Geoff) 0
    Assert Equal (Erc20 VTX TokenBalance Geoff) 1000000000000000300000

Test "VTX streaming can be changed for contributors"
    GrantsController
    InitSpeeds
    Controller Send "_setContributorVtxSpeed(address,uint256)" (Address Torrey) 300
    FastForward 1000 Blocks
    Controller Send "_setContributorVtxSpeed(address,uint256)" (Address Torrey) 600
    FastForward 1000 Blocks
    Controller UpdateContributorRewards Torrey
    Controller ClaimVtx Torrey
    Assert Equal (Controller VtxAccrued Torrey) 0
    Assert Equal (Erc20 VTX TokenBalance Torrey) 900000
    Controller Send "_setContributorVtxSpeed(address,uint256)" (Address Torrey) 0
    FastForward 2000 Blocks
    Assert Equal (Controller VtxAccrued Torrey) 0
    Assert Equal (Erc20 VTX TokenBalance Torrey) 900000
