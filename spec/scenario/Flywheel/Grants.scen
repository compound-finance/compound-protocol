-- Tests for the grants and math patch

Macro DeployComptroller price=1.0
    Unitroller Deploy
    PriceOracle Deploy Fixed price
    PriceOracleProxy Deploy Admin (PriceOracle Address) (Address Zero) (Address Zero) (Address Zero) (Address Zero) (Address Zero)
    ComptrollerImpl Deploy Scenario ComptrollerScen
    Unitroller SetPendingImpl ComptrollerScen
    ComptrollerImpl ComptrollerScen Become
    Comptroller SetPriceOracle (PriceOracleProxy Address)
    Comptroller SetCloseFactor 0.5
    Comptroller LiquidationIncentive 1.1

Macro SetupMarkets
    NewCToken ZRX cZRX
    NewCToken BAT cBAT
    Support cZRX collateralFactor:0.5
    Support cBAT collateralFactor:0.5

Macro SetupCompToken compInitAmount=5000000e18
    Erc20 Deploy Standard COMP "COMP Token" 18
    Give (Address Comptroller) compInitAmount COMP
    Comptroller Send "setCompAddress(address)" (Address COMP)

-- NewComptroller, but with markets listed so that we can make them comp markets in constructor
Macro FlywheelComptroller price=1.0 borrowRate=0.000005 compInitAmount=5000000e18
    DeployComptroller price
    SetupMarkets
    SetupCompToken compInitAmount

Macro InitUsage
    Prep Geoff 100e18 ZRX cZRX
    Mint Geoff 50e18 cZRX--tokenbalance = 50e18 / 2e9 = 2.5e10
    Prep Coburn Some BAT cBAT
    Mint Coburn 6e18 cBAT--tokenbalance = 6e18 / 2e9 = 3e9
    EnterMarkets Coburn cBAT
    Borrow Coburn 1e18 cZRX

Macro InitUsageAndSpeeds
    InitUsage
    Comptroller SetCompSpeeds (cZRX cBAT) (1 1) (1 1)

Test "COMP can be granted in combination with liquidity rewards"
    FlywheelComptroller
    InitUsageAndSpeeds
    Assert Equal (Comptroller CompAccrued Geoff) 0
    Assert Equal (Erc20 COMP TokenBalance Geoff) 0
    FastForward 1000 Blocks
    Comptroller ClaimComp Geoff
    Comptroller Send "_grantComp(address,uint256)" (Address Geoff) 1000
    Assert Equal (Comptroller CompAccrued Geoff) 0
    Assert Equal (Erc20 COMP TokenBalance Geoff) 2000 -- 1000 (grant) + 1000 (COMP supply rewards)

Test "COMP can be granted"
    -- Can be granted once
    FlywheelComptroller
    InitUsageAndSpeeds
    Assert Equal (Comptroller CompAccrued Geoff) 0
    Assert Equal (Erc20 COMP TokenBalance Geoff) 0
    Comptroller Send "_grantComp(address,uint256)" (Address Geoff) 1000
    Assert Equal (Comptroller CompAccrued Geoff) 0
    Assert Equal (Erc20 COMP TokenBalance Geoff) 1000
    -- Assert Log CompGranted (recipient (Address Geoff)) (amount "1000")
    -- Can be granted multiple times
    Comptroller Send "_grantComp(address,uint256)" (Address Geoff) 2000
    Assert Equal (Comptroller CompAccrued Geoff) 0
    Assert Equal (Erc20 COMP TokenBalance Geoff) 3000

Test "COMP can be streamed to contributors"
    FlywheelComptroller
    InitUsageAndSpeeds
    Assert Equal (Comptroller CompAccrued Torrey) 0
    Assert Equal (Erc20 COMP TokenBalance Torrey) 0
    Comptroller Send "_setContributorCompSpeed(address,uint256)" (Address Torrey) 300
    -- Assert Log ContributorCompSpeedUpdated (recipient (Address Torrey)) (amount "300")
    FastForward 1000 Blocks
    -- Just claimComp does not receive COMP
    Comptroller ClaimComp Torrey
    Assert Equal (Comptroller CompAccrued Torrey) 0
    Assert Equal (Erc20 COMP TokenBalance Torrey) 0
    -- Calling updateContributorRewards and then claimComp receives COMP
    Comptroller UpdateContributorRewards Torrey
    Assert Equal (Comptroller CompAccrued Torrey) 300000
    Comptroller ClaimComp Torrey
    Assert Equal (Comptroller CompAccrued Torrey) 0
    Assert Equal (Erc20 COMP TokenBalance Torrey) 300000

Test "COMP can be streamed in combination with liquidity rewards"
    FlywheelComptroller
    InitUsageAndSpeeds
    Comptroller Send "_setContributorCompSpeed(address,uint256)" (Address Geoff) 300
    FastForward 1000 Blocks
    -- Just claimComp does not receive COMP
    Comptroller UpdateContributorRewards Geoff
    Assert Equal (Comptroller CompAccrued Geoff) 300000
    Comptroller ClaimComp Geoff
    Assert Equal (Comptroller CompAccrued Geoff) 0
    Assert Equal (Erc20 COMP TokenBalance Geoff) 301000 -- 300000 (contributer grant) + 1000 (COMP supply rewards)

Test "COMP streaming can be changed for contributors"
    FlywheelComptroller
    InitUsageAndSpeeds
    Comptroller Send "_setContributorCompSpeed(address,uint256)" (Address Torrey) 300
    FastForward 1000 Blocks
    Comptroller Send "_setContributorCompSpeed(address,uint256)" (Address Torrey) 600
    FastForward 1000 Blocks
    Comptroller UpdateContributorRewards Torrey
    Comptroller ClaimComp Torrey
    Assert Equal (Comptroller CompAccrued Torrey) 0
    Assert Equal (Erc20 COMP TokenBalance Torrey) 900000
    Comptroller Send "_setContributorCompSpeed(address,uint256)" (Address Torrey) 0
    FastForward 2000 Blocks
    Assert Equal (Comptroller CompAccrued Torrey) 0
    Assert Equal (Erc20 COMP TokenBalance Torrey) 900000
