#!/usr/bin/env yarn repl -s

-- This script tests a hypothetical upgrade with manual VTX claims and manual VTX speed setting

PrintTransactionLogs
Alias VtxHolder "0xC89b6f0146642688bb254bF93C28fcCF1E182C81"
Alias USDCWhale "0x3dfd23a6c5e8bbcfc9581d2e864a68feb6a076d3"
Alias DAIWhale "0x47ac0Fb4F2D84898e4D9E7b4DaB3C24507a6D503"
Web3Fork "https://mainnet-eth.vortex.finance/@11458477" (VtxHolder USDCWhale DAIWhale)
UseConfigs mainnet

-- Deploy the flywheel impl
ControllerImpl Deploy Standard ControllerG7

-- Baseline USDC vtx speed before the patch
Assert Equal (Controller VtxSpeed cUSDC) 5.3786477206671197e16

-- Mint tokens
From DAIWhale (Trx GasPrice 0 (Erc20 DAI Approve vDAI UInt256Max))
From DAIWhale (Trx GasPrice 0 (VToken vDAI Mint 10000e6))
From USDCWhale (Trx GasPrice 0 (Erc20 USDC Approve cUSDC UInt256Max))
From USDCWhale (Trx GasPrice 0 (VToken cUSDC Mint 10000e6))

-- Baseline VTX claim speed
Assert Equal (Controller CheckIsVtxed vDAI) True

Controller ClaimVtx DAIWhale
AdvanceBlocks 1000
Expect Changes (Erc20 Vtx TokenBalance DAIWhale) 7.77e2
Controller ClaimVtx DAIWhale

-- Propose to apply the patch

From VtxHolder (Vtx Delegate VtxHolder)
From VtxHolder (Governor GovernorAlpha Propose "Disable automatic vtx speed refresh and automatic claims and change vtx speed" [(Address Unitroller) (Address ControllerG7) (Address Unitroller)] [0 0 0] ["_setPendingImplementation(address)" "_become(address)" "_setVtxSpeed(address,uint256)"] [[(Address ControllerG7)] [(Address Unitroller)] [(Address cUSDC) 30000000000000000]])

-- Vote for, queue, and execute the proposal

MineBlock
From VtxHolder (Governor GovernorAlpha Proposal LastProposal Vote For)
AdvanceBlocks 20000
Governor GovernorAlpha Proposal LastProposal Queue
IncreaseTime 604910
Governor GovernorAlpha Proposal LastProposal Execute
ControllerImpl ControllerG7 MergeABI

-- Check that speed has changed
Assert Equal (Controller VtxSpeed cUSDC) 3.0e16

-- Check that VTX is still accruing at the same speed
Controller ClaimVtx DAIWhale
AdvanceBlocks 1000
Expect Changes (Erc20 Vtx TokenBalance DAIWhale) 7.77e2
Controller ClaimVtx DAIWhale

-- Check that minting some USDC wont result in any automatic claims
AdvanceBlocks 1000000
From USDCWhale (Trx GasPrice 0 (Erc20 USDC Approve cUSDC UInt256Max))
Expect Changes (Erc20 Vtx TokenBalance USDCWhale) 0
From USDCWhale (Trx GasPrice 0 (VToken cUSDC Mint 10000e6))

-- Prepare second proposal
From VtxHolder (Governor GovernorAlpha Propose "Remove USDC market by setting vtx speed to 0" [(Address Unitroller)] [0] ["_setVtxSpeed(address,uint256)"] [[(Address cUSDC) 0]])

-- Vote for, queue, and execute the second proposal

MineBlock
From VtxHolder (Governor GovernorAlpha Proposal LastProposal Vote For)
AdvanceBlocks 20000
Governor GovernorAlpha Proposal LastProposal Queue
IncreaseTime 604910
Governor GovernorAlpha Proposal LastProposal Execute

AdvanceBlocks 1000

-- Check speed is now 0
Assert Equal (Controller VtxSpeed cUSDC) 0

-- True up balances
Controller ClaimVtx USDCWhale

-- Check no more vtx accrued
AdvanceBlocks 1000000
Expect Changes (Erc20 Vtx TokenBalance USDCWhale) 0
Controller ClaimVtx USDCWhale

-- Prepare final proposal
From VtxHolder (Governor GovernorAlpha Propose "Reset USDC vtx speed" [(Address Unitroller)] [0] ["_setVtxSpeed(address,uint256)"] [[(Address cUSDC) 27000000000000000]])

-- Vote for, queue, and execute the final proposal

MineBlock
From VtxHolder (Governor GovernorAlpha Proposal LastProposal Vote For)
AdvanceBlocks 20000
Governor GovernorAlpha Proposal LastProposal Queue
IncreaseTime 604910
Governor GovernorAlpha Proposal LastProposal Execute

-- Check new speed
Assert Equal (Controller VtxSpeed cUSDC) 2.7e16

Print "Upgrade OK!"
