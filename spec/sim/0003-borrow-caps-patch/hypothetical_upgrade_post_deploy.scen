#!/usr/bin/env yarn repl -s

PrintTransactionLogs
Alias CompHolder "0x19bc62ff7cd9ffd6bdced9802ff718f09f7259f1"
Alias USDCWhale "0x3dfd23a6c5e8bbcfc9581d2e864a68feb6a076d3"
Alias Arr00 "0x2b384212edc04ae8bb41738d05ba20e33277bf33"
Web3Fork "https://mainnet-eth.compound.finance/@10809638" (CompHolder USDCWhale)
UseConfigs mainnet


-- Propose to apply the patch

From CompHolder (Comp Delegate CompHolder)
From CompHolder (Governor GovernorAlpha Propose "Borrow Limit Comptroller Patch" [(Address Unitroller) (Address StdComptrollerG5) (Address Unitroller) (Address cSAI) (Address SAI)] [0 0 0 0 0] ["_setPendingImplementation(address)" "_become(address)" "_setBorrowCapGuardian(address)" "_reduceReserves(uint256)" "transfer(address,uint256)"] [[(Address StdComptrollerG5)] [(Address Unitroller)] [(Address CompHolder)] [2360000000000000000000] [(Address Arr00) 2360000000000000000000]])

-- Vote for, queue, and execute the proposal

MineBlock
From CompHolder (Governor GovernorAlpha Proposal LastProposal Vote For)
AdvanceBlocks 20000
Governor GovernorAlpha Proposal LastProposal Queue
IncreaseTime 604910
Governor GovernorAlpha Proposal LastProposal Execute
ComptrollerImpl StdComptrollerG5 MergeABI

Assert Equal (Address (Unitroller Implementation)) (Address StdComptrollerG5)
Assert Equal (Erc20 SAI TokenBalance (Address Arr00)) (2360000000000000000000)

From USDCWhale (Trx GasPrice 0 (Erc20 USDC Approve cUSDC UInt256Max))
From USDCWhale (Trx GasPrice 0 (CToken cUSDC Mint 100000000e6))
From USDCWhale (Trx GasPrice 0 (CToken cUSDC Borrow 1000000e6))

-- Market borrows were just under 81M at this block
From CompHolder (Comptroller SetMarketBorrowCaps (cUSDC) (68000000e6))

AllowFailures
From USDCWhale (Trx GasPrice 0 (CToken cUSDC Borrow 1000000e6))
Assert Revert

Successfully
From USDCWhale (Trx GasPrice 0 (CToken cUSDC RepayBorrow 1000000e6))
From USDCWhale (Trx GasPrice 0 (CToken cUSDC Borrow 10e6))



Print "Borrow Limit Comptroller Patch OK!"