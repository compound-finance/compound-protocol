#!/usr/bin/env yarn repl -s

PrintTransactionLogs

Alias Arr00 "0x2B384212EDc04Ae8bB41738D05BA20E33277bf33"
Alias CompHolder "0xC89b6f0146642688bb254bF93C28fcCF1E182C81"

Web3Fork "https://mainnet-eth.compound.finance/@12006351" (CompHolder)
UseConfigs mainnet

From CompHolder (Comp Delegate CompHolder)
From CompHolder (Governor GovernorAlpha Propose "Governor Bravo" [(Address Comptroller) (Address Timelock) (Address GovernorBravoDelegator)] [0 0 0] ["_grantComp(address,uint256)" "setPendingAdmin(address)" "_initiate(address)"] [[(Address Arr00) 2] [(Address GovernorBravoDelegator)] [(Address GovernorAlpha)]])

-- Vote for, queue, and execute the proposal
MineBlock
From CompHolder (Governor GovernorAlpha Proposal LastProposal Vote For)
AdvanceBlocks 20000
Governor GovernorAlpha Proposal LastProposal Queue
IncreaseTime 604910
Governor GovernorAlpha Proposal LastProposal Execute

Assert Equal (2) (Erc20 Comp TokenBalance (Address Arr00))
Assert Equal (Timelock Admin) (Address GovernorBravoDelegator)

From CompHolder (GovernorBravo GovernorBravoDelegator Propose "Grant Comp" [(Address Comptroller)] [0] ["_grantComp(address,uint256)"] [[(Address Arr00) 2]])

-- Vote for, queue, and execute the proposal
MineBlock
From CompHolder (GovernorBravo GovernorBravoDelegator Proposal LastProposal Vote For)
AdvanceBlocks 20000
GovernorBravo GovernorBravoDelegator Proposal LastProposal Queue
IncreaseTime 604910
GovernorBravo GovernorBravoDelegator Proposal LastProposal Execute

Assert Equal (4) (Erc20 Comp TokenBalance (Address Arr00))

-- Continuous proposal id count
Assert Equal (GovernorBravo GovernorBravoDelegator Proposal LastProposal Id) 41


Print "Migration to GovernorBravo Ok!"

